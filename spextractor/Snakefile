from os.path import *
import spextractor as spx
import numpy as np
from os.path import *


# infer wildcards from inputs
IDS, = glob_wildcards(join('input', '{id}.d'))


rule all:
    input:
        expand(join('output', 'ms2', '{id}.tsv'), id=IDS)


rule agilent2mzml:
    input:
        join('input', '{id}.d')
    output:
        join('output', 'raw', '{id}.mzML.gz')
    log:
        join('output', 'logs', 'agilent2mzml', '{id}.log')
    run:
        outdir = dirname(output[0])
        shell('msconvert.exe {input} -o {outdir} --32 --mz32 --inten32 -z -g --filter “defaultArrayLength 20-” > {log} 2>&1')


rule mzml2hdf:
    input:
        rules.agilent2mzml.output
    output:
        join('output', 'parsed', '{id}.h5')
    run:
        spx.process.mzml2hdf(input[0], output[0])


rule ms2:
    input:
        rules.mzml2hdf.output
    output:
        join('output', 'ms2', '{id}.tsv')
    run:
        outdir = dirname(output[0])

        if 'neg' in output[0]:
            mode = 'neg'
        else:
            mode = 'pos'

        tfix = config['calibration'][mode]['tfix']
        beta = config['calibration'][mode]['beta']

        shell('python {config[targeted][script]} {input} {outdir} {config[targeted][targets]} \
               --mode {mode} --tfix {tfix} --beta {beta}')
