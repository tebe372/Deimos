from os.path import *
import deimos
import numpy as np
from os.path import *
import pandas as pd
import glob


# infer wildcards from inputs
fns = [basename(x) for x in glob.glob(join('input', '*.*'))]
IDS = [splitext(splitext(x)[0])[0] for x in fns]
lookup = {k: v for k, v in zip(IDS, fns)}


rule all:
    input:
        expand(join('output', 'peakpick', '{id}.h5'), id=IDS)


rule mzml2hdf:
    input:
        lambda wildcards: join('input', lookup[wildcards.id])
    output:
        join('output', 'parsed', '{id}.h5')
    run:
        data = deimos.utils.read_mzml(input[0], accession=config['accession'])
        deimos.utils.save_hdf(output[0], data)


rule peakpick:
    input:
        rules.mzml2hdf.output
    output:
        join('output', 'peakpick', '{id}.h5')
    run:
        # ms1
        data = deimos.utils.load_hdf(input[0], level='ms1')
        partitions = deimos.utils.partition(data,
                                            split_on=config['partition']['split_on'],
                                            size=config['partition']['size'],
                                            overlap=config['partition']['overlap'])
        ms1_peaks = partitions.map(deimos.peakpick.auto,
                                   features=config['features'],
                                   processes=config['partition']['processes'],
                                   **config['peakpick']['ms1'])

        # ms2
        data = deimos.utils.load_hdf(input[0], level='ms2')
        partitions = deimos.utils.partition(data,
                                            split_on=config['partition']['split_on'],
                                            size=config['partition']['size'],
                                            overlap=config['partition']['overlap'])
        ms2_peaks = partitions.map(deimos.peakpick.auto,
                                   features=config['features'],
                                   processes=config['partition']['processes'],
                                   **config['peakpick']['ms2'])
        deimos.utils.save_hdf(output[0], {'ms1': ms1_peaks,
                                    'ms2': ms2_peaks})




