import deimos
import glob
import numpy as np
from os.path import *
import pandas as pd


# infer wildcards from inputs
fns = [basename(x) for x in glob.glob(join('input', '*.*'))]
IDS = [splitext(splitext(x)[0])[0] for x in fns]
lookup = {k: v for k, v in zip(IDS, fns)}


rule all:
    input:
        expand(join('output', 'peakpick', '{id}.h5'), id=IDS)


rule mzml2hdf:
    input:
        lambda wildcards: join('input', lookup[wildcards.id])
    output:
        join('output', 'parsed', '{id}.h5')
    run:
        # read/parse mzml
        data = deimos.read_mzml(input[0], accession=config['accession'])

        # save as hdf5
        deimos.save_hdf(output[0], data)


rule peakpick:
    input:
        rules.mzml2hdf.output
    output:
        join('output', 'peakpick', '{id}.h5')
    run:
        # ms1
        # load data
        data = deimos.load_hdf(input[0], key='ms1')

        # partition in m/z
        partitions = deimos.partition(data,
                                      split_on=config['partition']['split_on'],
                                      size=config['partition']['size'],
                                      overlap=config['partition']['overlap'])

        # perform peakpicking per partition
        ms1_peaks = partitions.map(deimos.peakpick.local_maxima,
                                   dims=config['dims'],
                                   processes=config['partition']['processes'],
                                   **config['peakpick']['ms1'])
        deimos.save_hdf(output[0], ms1_peaks, key='ms1')

        # ms2
        # load data
        data = deimos.load_hdf(input[0], key='ms2')

        # partition in m/z
        partitions = deimos.partition(data,
                                      split_on=config['partition']['split_on'],
                                      size=config['partition']['size'],
                                      overlap=config['partition']['overlap'])

        # perform peakpicking per partition
        ms2_peaks = partitions.map(deimos.peakpick.local_maxima,
                                   dims=config['dims'],
                                   processes=config['partition']['processes'],
                                   **config['peakpick']['ms2'])
        deimos.save_hdf(output[0], ms2_peaks, key='ms2')
